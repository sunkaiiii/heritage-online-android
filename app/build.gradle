apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 31

    def value = 0
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def code = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = code.toString()
        def runTasks = gradle.startParameter.taskNames
        versionProps.store(versionPropsFile.newWriter(), null)
        defaultConfig {
            applicationId "com.datong.heritage_online"
            minSdkVersion 23
            targetSdkVersion 31
            versionCode code
            versionName "5.0.0"
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            resConfigs "zh", "en"
            renderscriptTargetApi 31
            renderscriptSupportModeEnabled true
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }
    buildFeatures{
        viewBinding true
        compose true
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //Other parameters
            debuggable false
            minifyEnabled true
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
            shrinkResources true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    composeOptions {
        kotlinCompilerVersion "1.5.21"
        kotlinCompilerExtensionVersion "1.0.2"
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        checkDependencies true
    }
    buildToolsVersion '31.0.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    def nav_version = "2.4.2"
    //    android support
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "com.google.android.material:material:1.5.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"
    implementation 'com.google.dagger:hilt-android:2.41'
    kapt 'com.google.dagger:hilt-compiler:2.41'
    //    network
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.github.bumptech.glide:glide:4.13.1'
    kapt 'com.github.bumptech.glide:compiler:4.13.1'
    //    view
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    //Persistence
    def room_version="2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Jetpack Compose
    def compose_version = "1.1.1"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1'
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.runtime:runtime-rxjava2:$compose_version"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.0"
    implementation "androidx.paging:paging-compose:1.0.0-alpha14"
    implementation 'io.coil-kt:coil-compose:1.3.2'

}
repositories {
    mavenCentral()
    google()
}
